#!/usr/bin/perl

use strict;
use warnings;

use FindBin;

use lib "$FindBin::Bin/lib/";

use JesMarkdown qw(markdown);
use POSIX qw(strftime);

my $indir = "..";
my $outdir = "html";
my $templatedir = "templates";

mkdir $outdir;

my $HEADER = slurp("$templatedir/header");
my $STORYHEADER = slurp("$templatedir/storyheader");
my $FOOTER = slurp("$templatedir/footer");
my $STORYFOOTER = slurp("$templatedir/storyfooter");
my $PREVIEW = slurp("$templatedir/preview");
my $GALLERYITEM = slurp("$templatedir/galleryitem");

my $NAVHTML = navhtml(Index => 'index.html') . navhtml('Gallery' => 'gallery.html') . ' . ';

my @stories;
for my $file (sort glob("$indir/*.md")) {
    (my $name = $file) =~ s/^\Q$indir\E\///;
    $name =~ s/\.md$//;

    my ($modified, $commit) = split / /, `git log -1 --pretty="format:%ct %H" \Q$file\E`;
    $modified //= 'never';
    $commit //= '';

    my $story = {
        file => $file,
        name => $name,
        modified => $modified,
        commit => $commit,
        markdown => slurp($file),
        # title => either "# title" from first line of markdown, else $name
    };

    if ($story->{markdown} =~ /^# (.*)/) {
        $story->{title} = $1;
    } else {
        $story->{title} = $name;
    }

    if ($name =~ /^(20\d\d)([01]\d)([0123]\d)$/) {
        my ($year, $month, $day) = ($1, $2, $3);
        die "$name: $year/$month/$day: title ($story->{title}) does not match filename" if $story->{title} ne "$year-$month-$day";
        $story->{daily} = 1;
    }

    $NAVHTML .= navhtml($story->{title}, "$story->{name}.html") if !$story->{daily};

    push @stories, $story;
}

for my $i (0..$#stories) {
    my $story = $stories[$i];
    next if !$story->{daily};
    my $next = $stories[$i+1];
    my $prev = $stories[$i-1];
    $story->{next} = $next if $next->{daily};
    $story->{prev} = $prev if $prev->{daily};
}

write_story($_) for @stories;
write_index(@stories);
write_gallery(@stories);

sub write_story {
    my ($story) = @_;

    # strip title
    my $markdown = $story->{markdown};
    $markdown =~ s/^(# .*)//;

    my $html = $HEADER . $STORYHEADER . md2html($markdown) . $STORYFOOTER . $FOOTER;
    spit("$outdir/$story->{name}.html", template($html, $story));
}

sub write_index {
    my (@stories) = @_;

    my $html = '';

    my $month = '';

    for my $story (sort { $b->{name} cmp $a->{name} } @stories) {
        next if !$story->{daily};
        if ($story->{title} =~ /^(\d{4}-\d{2})/) {
            my $newmonth = $1;
            if ($month ne $newmonth) {
                $html .= "<h2>$newmonth</h2>";
                $month = $newmonth;
            }
        }
        $html .= template($PREVIEW, $story);
    }

    spit("$outdir/index.html", template($HEADER . $html . $FOOTER, {
        title => 'jes notes',
    }));
}

sub write_gallery {
    my (@stories) = @_;

    my $html = '';

    my %image;

    for my $story (sort { $b->{name} cmp $a->{name} } @stories) {
        while ($story->{markdown} =~ /(https:\/\/img\.incoherency\.co\.uk\/\d+)/g) {
            push @{ $image{$1} }, $story if ! grep { $_ eq $story } @{ $image{$1} };
        }
    }

    for my $url (reverse sort keys %image) {
        my $storieshtml = '<ul>' . join('', map { "<li><a href=\"$_->{name}.html\">$_->{title}</a></li>" } @{ $image{$url} }) . '</ul>';
        $html .= template($GALLERYITEM, {url => $url, stories => $storieshtml});
    }

    spit("$outdir/gallery.html", template($HEADER . $html . $FOOTER, {
        title => 'Gallery',
    }));
}

sub template {
    my ($html, $story) = @_;
    $html =~ s/__TITLE__/$story->{title}/g;
    $html =~ s/__NAME__/$story->{name}/g;
    $html =~ s/__LAST_MODIFIED__/strftime("%Y-%m-%d %H:%M:%S", gmtime($story->{modified}))/ge;
    $html =~ s/__LAST_COMMIT__/$story->{commit}/g;
    $html =~ s/__NAV__/$NAVHTML/g;
    $html =~ s/__PREVIEW__/previewhtml($story)/ge;
    $html =~ s/__CONTENTS__/contentshtml($story)/ge;
    $html =~ s/__URL__/$story->{url}/g;
    $html =~ s/__STORIES__/$story->{stories}/g;
    $html =~ s/__PREV_NEXT__/prevnexthtml($story)/ge;
    return $html;
}

sub slurp {
    my ($name) = @_;
    open (my $fh, '<', $name)
        or die "can't read $name: $!\n";
    my $c = join('', <$fh>);
    close $fh;
    return $c;
}

sub spit {
    my ($name, $c) = @_;
    open (my $fh, '>', $name)
        or die "can't write $name: $!\n";
    print $fh $c;
    close $fh;
}

sub md2html {
    my ($md) = @_;

    # fix un-hyperlinked hyperlinks
    $md =~ s/(?<!\()(https?:\/\/\S+)/[$1]($1)/g;

    my $html = markdown($md);

    # fix relative md hyperlinks
    $html =~ s/href="([^\/"]+)\.md"/href="$1.html"/g;

    # fix imagebin links
    $html =~ s/<a href="https:\/\/img\.incoherency\.co\.uk\/(\d+)">[^<]+<\/a>/<div class="img-container"><a class="img-link" href="https:\/\/img.incoherency.co.uk\/$1"><img src="https:\/\/img.incoherency.co.uk\/$1\/thumb"><\/a><\/div>/g;

    return $html;
}

sub previewhtml {
    my ($story) = @_;
    return join(', ', headings($story));
}

sub contentshtml {
    my ($story) = @_;
    my @h = headings($story);
    return "" if @h < 2;

    my $html = '<ul>';
    for my $i (0 .. $#h) {
        $html .= '<li>';
        $html .= "<a href=\"#markdown-heading-$i\">$h[$i]</a>";
        $html .= '</li>';
    }
    $html .= "</ul>\n";
    return $html;
}

sub prevnexthtml {
    my ($story) = @_;

    my $html = '';
    $html .= "<a href=\"$story->{prev}{name}.html\">&lt; $story->{prev}{title}</a> " if $story->{prev};
    $html .= "<a href=\"$story->{next}{name}.html\">$story->{next}{title} &gt;</a> " if $story->{next};
    return $html;
}

sub headings {
    my ($story) = @_;
    return map { s/^#+\s*//; $_ }
           grep { /^##[^#]/ }
           split /\n/, $story->{markdown};
}

sub lastmodified {
    my ($story) = @_;

    # in markdown format
    return "*Last modified: [" . strftime("%Y-%m-%d %H:%M:%S", gmtime($story->{modified})) . "](https://github.com/jes/notes/commit/$story->{commit})*\n";
}

sub navhtml {
    my ($title, $file) = @_;

    return "<a class=\"nav\" href=\"$file\">$title</a> ";
}
